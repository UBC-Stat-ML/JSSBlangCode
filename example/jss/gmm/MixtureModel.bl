package jss.gmm

model MixtureModel { 
  
  random  List<RealVar>  observations 
  random  List<IntVar>   labels  ?:  latentIntList(observations.size) 
  random  Simplex        pi                 ?:  latentSimplex(2)
  random  List<RealVar>  means              ?:  latentRealList(2)
  random  List<RealVar>  variances          ?:  latentRealList(2)
  param   Matrix         concentration      ?:  fixedVector(1.0, 1.0)
  
  laws {
     
    pi | concentration ~ Dirichlet(concentration)
    
    for (int mixIdx : 0 ..< means.size) { 
      means.get(mixIdx)     ~ Normal(0.0, pow(10,2.0))
      variances.get(mixIdx) ~ ContinuousUniform(0.0, 10.0)
    }
    
    for (int obsIdx : 0 ..< observations.size) {

      labels.get(obsIdx) | pi ~ Categorical(pi)

      observations.get(obsIdx) |
        means, variances, 
        IntVar curIndic = labels.get(obsIdx)
        ~ Normal(
          means.get(curIndic),
          variances.get(curIndic)
        )
    }
  }
}
